plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    // 코틀린
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.spring'
    id 'org.jetbrains.kotlin.plugin.jpa'
}

group = 'org.delivery'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


repositories {
    mavenCentral()
}

dependencies {
    //mq의 Producer역활을 위한
    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation project(':db')
    implementation project(':common')

    //Swagger
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.7.0'
    //Provides transitive vulnerable dependency maven:org.apache.commons:commons-lang3:3.17.0 CVE-2025-48924 5.3 Insufficient Information Results powered by Mend.io
    //위의 경고문구가 발생하여 안전한 버전 명시
    implementation 'org.apache.commons:commons-lang3:3.18.0'


    // JWT
    implementation("io.jsonwebtoken:jjwt-api:0.11.5")
    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.11.5")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.11.5")

    // Kotlin
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'

}
// Kotlin 컴파일러 옵션 설정 [Java의 @Nullable, @NonNull 어노테이션을 엄격하게 해석, 자바 라이브러리 사용 시 null 안전성을 더 강하게 체크]
kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

// 특정 어노테이션이 붙은 클래스를 자동으로 open 으로 만듬 [코틀린으로 만든 코드를 JPA가 인지할수있도록]
allOpen {
    annotation 'jakarta.persistence.Entity'
    annotation 'jakarta.persistence.MappedSuperclass'
    annotation 'jakarta.persistence.Embeddable'
}



test {
    useJUnitPlatform()
}

bootJar{
    enabled = true
}
jar{
    enabled = false
}